#[
	    E2 Image Viewer - Potat05 & musicalskele    
              E2 QOI Decoder - musicalskele                     
                                                                                     
   Be cautious when loading large image data (>=250KiB)       
      as it may lead to crashes on lower-end systems.          
                                                                                     
                                                                                     
  If you encounter issues rendering the image, use the        
    "wire_expression2_unlimited 1" console command to           
          disable the E2 ops limit temporarily.                
                                                                                     
   (Last Update: February 18, 2024)
   Supported Formats:
   RGB888, BC1
   Partially-Supported Formats:
   RGB223, RGB565, YCbCr (untested)
   QOI (heavily tested)
   Using QOI is heavily recommended                                          
]#


@name skele's advanced display component 

@strict

@inputs DigiScreen:wirelink ImageStream:string

@persist [Width, Height, X, Y]:number
@persist [Start, CurrentState,CPUMax,OPSMax]:number
@persist [Base64Stream, Base64Charset]:string 
@persist [CONS_ONE_THIRD,CONS_TWO_THIRDS,CONS_HALF]:number
@persist [DecodedArray]:array
@persist ConfigsTable:table
@persist Owner:entity
@persist Base64Stored:array
@persist CmdPrefix:string
@persist SeenPixels:array
@persist RunLength:number
@persist Index:number
@persist [DeltaR,DeltaG,DeltaB]:number
@persist [DRDG,DBDG]:number
@persist SeenPixels:array
@persist CurrentByte:number
@persist [PrevPixel,Color]:vector4
@persist HashedColor:number

    const QOI_HEADER_SIZE = 14
    const QOI_END_MARKER = array(0, 0, 0, 0, 0, 0, 0, 1)

    const QOI_END_MARKER_SIZE = QOI_END_MARKER:count()


    const QOI_OP_RUN   = 0xC0
    const QOI_OP_INDEX = 0x00
    const QOI_OP_DIFF  = 0x40
    const QOI_OP_LUMA  = 0x80
    const QOI_OP_RGB   = 0xFE
    const QOI_OP_RGBA  = 0xFF

    const QOI_CHUNK_MASK = 0xC0
    const QOI_RUN_LENGTH = 0x3F
    const QOI_INDEX      = 0x3F

    const QOI_INDEX_MASK = 0x30
    const QOI_DIFF_RED = 0x03
    const QOI_DIFF_GREEN = 0x0F
    const QOI_DIFF_BLUE = 0x3C

    const QOI_LUMA_MASK = 0xF0
    const QOI_LUMA_GREEN = 0x3F
    const QOI_LUMA_DRDG = 0xF0
    const QOI_LUMA_DBDG = 0x0F



if(first() || dupefinished()) {
    ConfigsTable=table(
    "CPUMax"          = 10000, 
    "OPSMax"          = 11000, 
    "CmdPrefix"       = ">",    
    "ResetDuringInit" = 1) 

  #---------|CONFIGS|---------#
    CPUMax          = 10000   # 2k or higher
    OPSMax          = 11000   # ~10k
    CmdPrefix       = ">"     # command prefix
    ResetDuringInit = 1       #
  #--------|CONSTANTS|--------#
    CONS_ONE_THIRD  = 1/3
    CONS_TWO_THIRDS = 2/3     # not actually sure if these help with efficiency. may remove in future.
    CONS_HALF       = 1/2
  #----------|MISC.|----------#
    Width           =0
    Height          =0
    X               =0
    Y               =0
    Format          =0
    CurrentState    =0
  #-----------|QOI|-----------#

  #--------|IMAGE DATA|--------#

    # Imagedata can either be:    
    # included from a seperate file (heavily recommended)
    #include "imagedata"
    
    # an input
    # Base64Stream = ImageStream

    # or pasted directly here (do not attempt for files >100kb)
    # Base64Stream =

  #----------------------------#




}


  #------|CHAT COMMANDS!|------#

Owner = owner()

event chat(Player:entity, Message:string, _:number) {
      # basically ripped from the expression 2 events section from wiremod wiki, pretty much wip
    if (Player == Owner & Message[1] == CmdPrefix) {
        local Rest = Message:sub(2)
        local Get = Rest:explode(" ")
        switch (Get[1, string]) {
            
            case "info",
                hideChat(1)
                local FormatString = array(
                    "None",
                    "RGB223",
                    "RGB565",
                    "YCbCr",
                    "BC1",
                    "RGB888",
                    "QOI"
                )[Format + 1, string]
                
                print("E2 Image Display by Potat05 & musicalskele - Currently Displaying image of size " + floor(Base64Stream:length() / 1024) + "KiB or "+round(Base64Stream:length() / 1048576,3)+"MiB, with dimensions " + Width + ":" + Height + " size, with the " + FormatString + "format.")
                break
            case "init",
                hideChat(1)
                print("reinitializing the display.")
                Base64CharIndex = 1
                Base64Stored = array()
                CurrentState=1
                break
            #case "config",
            #    hideChat(1)
            #    print(Get)
            #    break
            default,
                print("that is an unknown command: " + Get[1, string])
                break
        }
    }

    if((CurrentState == 0) && (DigiScreen != nowirelink())) {
        CurrentState = 1
    }
}

if((CurrentState == 0) && ~DigiScreen && (DigiScreen != nowirelink())) {
    CurrentState = 1
}


  #------|DISPLAY FUNCT.|------#

# Color mode functions:
function wirelink:dsGetColorMode() { return This[1048569] }  # Retrieves the current color mode
function wirelink:dsSetColorMode(Mode:number) { This[1048569] = Mode}  # Sets the color mode
#[ 
    Color modes:
    | 0: RGBXXX
    | 1: R G B
    | 2: R*65536 + G*256 + B
    | 3: RRRGGGBBB 
]#

# Digital Screen clearing functions:
function wirelink:dsClearRow(Row:number) { This[1048570] = Row }  # Clears a specific row
function wirelink:dsClearColumn(Column:number) { This[1048571] = Column }  # Clears a specific column
function wirelink:dsClear() { This[1048574] = 0 }  # Clears the entire display

# Digital Screen write functions:
function wirelink:dsSetPixel(X:number,Y:number, Col:vector) {
  # TODO: Is there a way to write to mipmaps?
  # This[X * Height + Y] = rgb2digi(Col, 2)
    This[X + Y * Height] = rgb2digi(Col, 2) 
}

# Digital Screen dimension functions:
function wirelink:dsGetHeight() { return This[1048572] }  # Retrieves the display height
function wirelink:dsSetHeight(Height:number) { This[1048572] = Height }  # Sets the display height
function wirelink:dsGetWidth() { return This[1048573] }  # Retrieves the display width
function wirelink:dsSetWidth(Width:number) { This[1048573] = Width }  # Sets the display width

# Other Digital Screen functions:
function wirelink:dsClock() {This[1048575] = 1}  # Clock function 
function wirelink:dsReset() {
    if(ResetDuringInit){This[1048574] = 0} # Reset Screen and whatever was on it will be cleared.
    This[1048569] = 2       # Set color mode
    This[1048575] = 1       # Apply changes
                            # Set resolution
    This[1048572] = Width   # Set width
    This[1048573] = Height  # Set height
}


  #------|BASE-64 FUNCT.|------#

Base64Charset = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/"
Base64CharIndex = 1
Base64Stored = array()

function number b64_next() {
    
    if(Base64Stored:count() == 0) {
        
        local N =
            bShl(Base64Charset:find(Base64Stream[Base64CharIndex]) - 1, 18) +
            bShl(Base64Charset:find(Base64Stream[Base64CharIndex + 1]) - 1, 12) +
            bShl(Base64Charset:find(Base64Stream[Base64CharIndex + 2]) - 1, 6) +
            (Base64Charset:find(Base64Stream[Base64CharIndex + 3]) - 1)
        
        
        Base64Stored:pushNumber(bAnd(bShr(N, 16), 0xFF))
        Base64Stored:pushNumber(bAnd(bShr(N, 8), 0xFF))
        Base64Stored:pushNumber(bAnd(N, 0xFF))
        
        Base64CharIndex += 4
        
    }
    
    return Base64Stored:shiftNumber()
    
}


  #-|PIXEL & INCREMENT FUNCT.|-#

function number incrementNPixels(N:number) {
    for (I = 1, N) {
        X++
        if(X >= Width) {
            X = 0
            Y++
            if(Y >= Height) {
                Y = 0
                return 1
            }
        }
    }
    return 0
}


function number incrementSingularPixel() {
    X++
    if(X >= Width) {
        X = 0
        Y++
        if(Y >= Height) {
            Y = 0
            return 1
        }
    }
    
    return 0
}

function number incrementPixelCheck() {
    let XTemp=X
    let YTemp=Y
    XTemp++
    if(XTemp >= Width) {
        XTemp = 0
        YTemp++
        if(YTemp >= Height) {
            YTemp = 0
            return 1
        }
    }
    
    return 0
}

function number incrementBlock(Size:number) {
    X += Size
    if(X >= Width) {
        X = 0
        Y += Size
        if(Y >= Height) {
            Y = 0
            return 1
        }
    }
    
    return 0
}


  #-----|PIXEL OPERATIONS|-----#

function vector ycbcr_rgb(Y:number, Cb:number, Cr:number) {
    # https://en.wikipedia.org/wiki/YCbCr#JPEG_conversion
    # Usually clamping these is useless, But because we quantize to save bits
    # we need to make sure these are clamped or else artifacts will appear.
    return vec(
        clamp(Y                         + 1.402    * (Cr - 128), 0, 255),
        clamp(Y - 0.344136 * (Cb - 128) - 0.714136 * (Cr - 128), 0, 255),
        clamp(Y + 1.772    * (Cb - 128)                        , 0, 255)
    )
}

function number dequantize(Value:number, Bits:number) {
    return (Bits == 5) * (Value * 33 >> 2) + (Bits == 6) * (Value * 65 >> 4) + (Bits != 5 && Bits != 6) 
}

function vector decode565(Color:number) {
    return vec(
        dequantize((Color & 0b1111100000000000) >> 11, 5),
        dequantize((Color & 0b0000011111100000) >> 5, 6 ),
        dequantize(Color & 0b0000000000011111, 5        )
    )
}


  #------|QOI RELATED FUNCTIONS AND VARIABLES|------#

function number readByte() {return b64_next()}
        
function number read32() {
    return (b64_next() << 24 || b64_next() << 16 || b64_next() << 8 || b64_next() << 0) 
    }

function fQOI_DecodeHeader() {

    HeaderMagic=read32()
    if (HeaderMagic != 0x716F6966) {
        printColor(vec(255,0,0),"Invalid QOI Format, decoding may not work!")
    }

    ReadWidth      = read32()
    ReadHeight     = read32()
    ReadChannels   = readByte()
    ReadColorSpace = readByte()

    print("QOI File Header Dump:")
    print("Width:"+ReadWidth+" Height:"+ReadHeight+" Channels:"+ReadChannels+" Colorspace:"+ReadColorSpace )
}

# is there a better way to do this? looks so ugly.
SeenPixels=array(
                vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),
                vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),
                vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),
                vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),
                vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),
                vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),
                vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),
                vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0),vec4(0,0,0,0)
)

PrevPixel          = vec4(0,0,0,255)
Pixels2Return      = array()
ReturnedPixelIndex = 0

function void pushPixel(Color:vector4) {
    ReturnedPixelIndex++
    # Increase the Index
    let R = Color:x()
    let G = Color:y()
    let B = Color:z()
    # Declare R,G,B
    PrevPixel   = Color
    HashedColor = (R * 3 + G * 5 + B * 7 + 53) % 64
    SeenPixels:setVector4(HashedColor,Color)
    #2805 is 255*11, we assume all pixels are solid, mod 2805 is 53
    Pixels2Return:setVector(ReturnedPixelIndex,vec(R,G,B))
    
}

function array fQOI_DecodePixel() {
    # Read a Byte
    CurrentByte = readByte()
    
    # Reset PixelIndex and RunLength 
    ReturnedPixelIndex = 0
    RunLength = 1
    
    # Check 8 bit opcodes
    switch (CurrentByte) {
    case QOI_OP_RGB,
        let R = readByte()
        let G = readByte()
        let B = readByte()
        # Always return 255 for Alpha
        pushPixel(vec4(R,G,B,0xFF))
        return array(0,PrevPixel)
    case QOI_OP_RGBA,
        let R = readByte()
        let G = readByte()
        let B = readByte()
        let A = readByte()
        # Also always return 255 for Alpha, it's pretty much useless..
        pushPixel(vec4(R,G,B,0xFF))
        return array(0,PrevPixel)
    default,
        break
    }

    # Check 2 bit opcodes
    switch (CurrentByte && QOI_CHUNK_MASK) {
            case QOI_OP_RUN,
                RunLength = (CurrentByte && 0b00111111)+1
                for (I = 1, RunLength) {
                    pushPixel(PrevPixel)
                }
                return array(RunLength,PrevPixel)
                
            case QOI_OP_INDEX,
                pushPixel(SeenPixels[CurrentByte,vector4])
                return array(0,PrevPixel)
                
            case QOI_OP_DIFF,
                DeltaR = ((CurrentByte && 0b00110000) >> 4) - 2
                DeltaG = ((CurrentByte && 0b00001100) >> 2) - 2
                DeltaB = ((CurrentByte && 0b00000011)     ) - 2

                let ReconstructedPixel = vec4(
                    PrevPixel:x()+DeltaR,
                    PrevPixel:y()+DeltaG,
                    PrevPixel:z()+DeltaB,
                    0xFF)
                    
                pushPixel(ReconstructedPixel)
                return array(0,ReconstructedPixel)

            case QOI_OP_LUMA,
                CurrentByte2 = readByte()

                DeltaG = ((CurrentByte  && 0b00111111)     ) - 32
                DRDG   = ((CurrentByte2 && 0b11110000) >> 4) - 8
                DBDG   = ((CurrentByte2 && 0b00001111)     ) - 8

                let ReconstructedPixel = vec4(
                    PrevPixel:x()+DeltaG+DRDG,
                    PrevPixel:y()+DeltaG     ,
                    PrevPixel:z()+DeltaG+DBDG,
                    0xFF)
                    
                pushPixel(ReconstructedPixel)
                return array(0,ReconstructedPixel)
                
            default,
                print("Unrecognized 2bit opcode, that's literally impossible wtf")
                break
    }
    return array(0,vec4(0,0,0,0))
}

  #-----|NEXTPIXELS FUNC.|-----#
function number nextPixels() {
    if (X >= Width) {
        return 1
    }
    
    switch(Format) {
        case 1,
          #---------|RGB-233|---------#
            local Color = b64_next()
            DigiScreen:dsSetPixel(X, Y, vec(
                bAnd(Color, 0b11000000),
                bShl(bAnd(Color, 0b00111000), 2),
                bShl(bAnd(Color, 0b00000111), 5)
            ))
            return incrementSingularPixel()
        
        case 2,
          #---------|RGB-565|---------#
            local Color = bOr(b64_next(), bShl(b64_next(), 8))
            DigiScreen:dsSetPixel(X, Y, decode565(Color))
            return incrementSingularPixel()

        case 3,
          #--------|YCbCr-655|--------#
            local Color = bOr(b64_next(), bShl(b64_next(), 8))
            DigiScreen:dsSetPixel(X, Y, ycbcr_rgb(
                dequantize(bShr(bAnd(Color, 0b1111110000000000), 10), 6),
                dequantize(bShr(bAnd(Color, 0b0000001111100000), 5), 5),
                dequantize(bAnd(Color, 0b0000000000011111), 5)
            ))
            return incrementSingularPixel()
        
        case 4,
          #-----------|BC1|-----------#
            local Color1Value = bOr(b64_next(), bShl(b64_next(), 8))
            local Color2Value = bOr(b64_next(), bShl(b64_next(), 8))
            local Color1 = decode565(Color1Value)
            local Color2 = decode565(Color2Value)
            

            local Palette = (Color1Value > Color2Value) ? array(
                Color1,
                Color2,
                lerp(Color1, Color2, CONS_ONE_THIRD),
                lerp(Color1, Color2, CONS_TWO_THIRDS)
            ) : array(
                Color1,
                Color2,
                lerp(Color1, Color2, CONS_HALF),
                vec(0, 0, 0)
            )
			
            for(Dy = 0, 3, 1) {
                if (Y + Dy < Height) {
                    local BitmapRow = b64_next()
                    for(Dx = 0, 3, 1) {
                        if (X + Dx < Width) {
                            local P = bAnd(bShr(BitmapRow, Dx * 2), 0b11)+1
                            DigiScreen:dsSetPixel(X + Dx, Y + Dy, Palette[P, vector])
                        }
                    }
                }
            }
            return incrementBlock(4)

        case 5,
          #---------|RGB-888|---------#
            DigiScreen:dsSetPixel(X, Y, vec(b64_next(),b64_next(),b64_next()))
            return incrementSingularPixel()
            
        case 6,
          #-----------|QOI|-----------#
            ReturnedPixels=fQOI_DecodePixel()
            XTemp=X
            YTemp=Y
            for (I = 1, ReturnedPixels[1,number]+1) {
                Vec3Color=vec(ReturnedPixels[2,vector4])
                DigiScreen:dsSetPixel(XTemp, YTemp,Vec3Color)
                XTemp++
                if((XTemp) >= Width) {
                    XTemp = 0
                    YTemp++
                }
            }
            return incrementNPixels(RunLength)
          
        default,
            # handle unknown format or other cases
            return 0
    }
}

  #-----|MAIN LOOP TICK|-----#
Start = 0
event tick(){
   
    switch(CurrentState) {
        
        case 0, break
        
        case 1,
            Start = systime()
            Width = bOr(b64_next(), bShl(b64_next(), 8))
            Height = bOr(b64_next(), bShl(b64_next(), 8))
            Format = b64_next()
            DigiScreen:dsReset()
            local FormatString = array(
                "None",
                "RGB223",
                "RGB565",
                "YCbCr",
                "BC1",
                "RGB888"
                )[Format + 1, string]
            print("E2 Image Display Component by Potat05 & musicalskele - Displaying image of size " + floor(Base64Stream:length() / 1024) + "KiB or "+round(Base64Stream:length() / 1048576,3)+"MiB, with dimensions " + Width + ":" + Height + " size, with the " + FormatString + "format.")
            if (Format == 6) {
                fQOI_DecodeHeader()
            }

            CurrentState = 2
        break
        
        case 2,
            while(opcounter()<OPSMax&cpuUsage()*100000<CPUMax){
                if(nextPixels()) {
                    CurrentState = 3
                    break
                }
            }
        break
        
        case 3,
            print("E2 Image Display by Potat05 & musicalskele - Finished in " + 1000*round(systime() - Start,5) + "ms, you may delete the e2 chip now.")
            CurrentState = 4
        break
            
        case 4, break

            
}}

